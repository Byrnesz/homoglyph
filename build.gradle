group = "net.codebox"
version = "1.3.0"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceSets.main.resources {
    srcDirs = ["raw_data"]; include "char_codes.txt"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Homoglyph'
                description = 'Library providing a homoplyph-aware text search function'
                url = 'https://github.com/codebox/homoglyph'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'codebox'
                        name = 'Rob Dawson'
                        email = 'rob@codebox.net'
                    }
                }
                scm {
                    connection = 'scm:git@github.com/codebox/homoglyph.git'
                    developerConnection = 'scm:git@github.com/codebox/homoglyph.git'
                    url = 'https://github.com/codebox/homoglyph'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
